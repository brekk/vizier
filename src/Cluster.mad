import type { Edge } from "@/Edge"
import type { Rect } from "@/Geometry"
import type { Node } from "@/Node"

import List from "List"
import { join } from "String"

import { attr } from "@/Dot"
import { renderEdges } from "@/Edge"
import { Rect } from "@/Geometry"
import { renderNodes, renderNodesWithIndent } from "@/Node"
import { indent } from "@/String"



alias Color = String

export type ClusterStyle = ClusterStyleFilled | ClusterStyleStriped | ClusterStyleRounded

export styleLiteralToString = where {
  ClusterStyleFilled =>
    "filled"

  ClusterStyleStriped =>
    "striped"

  ClusterStyleRounded =>
    "rounded"
}

export stringToStyleLiteral = (x) => x == "filled"
  ? ClusterStyleFilled
  : x == "striped" ? ClusterStyleStriped : ClusterStyleRounded

export alias Layer = String
export type Justification = JustificationLeft | JustificationCenter | JustificationRight

export justificationToString = where {
  JustificationLeft =>
    "left"

  JustificationCenter =>
    "center"

  JustificationRight =>
    "right"
}

export type ClusterAttribute
  = Area(Float)
  | Bb(Rect)
  | Bgcolor(Color)
  | Class(String)
  | ClusterAttr(Boolean)
  | Color(Color)
  | Colorscheme(String)
  | Fillcolor(Color)
  | Fontcolor(Color)
  | Fontname(String)
  | Fontsize(Float)
  | Gradientangle(Integer)
  | Href(String)
  | Id(String)
  | K(Float)
  | Label(String)
  | Labeljust(Justification)
  | Labelloc(String)
  | Layer(String)
  | Lheight(Float)
  | Lp(String)
  | Lwidth(Float)
  | Margin(Float)
  | Nojustify(Boolean)
  | Pencolor(Color)
  | Penwidth(Float)
  | Peripheries(Integer)
  | Sortv(Integer)
  | Style(ClusterStyle)
  | Target(String)
  | Tooltip(String)
  | URL(String)
  | UnmatchedClusterAttribute

export type Cluster = Cluster(String, List ClusterAttribute, List Node, List Edge)

toString :: Integer -> Cluster -> String
export toString = (i, c) => where(c) {
  Cluster(label, attrs, nodes, edges) =>
    `
  subgraph cluster_${show(i)} {
    label = "${label}";
    ${renderAttributes(attrs)}
    ${renderNodesWithIndent(2, nodes)}
    ${renderEdges(edges)}
  }`
}

renderClusters :: List Cluster -> String
export renderClusters = pipe(
  List.mapWithIndex((x, i) => toString(i, x)),
  join("\n"),
)

attributeToString :: ClusterAttribute -> String
export attributeToString = where {
  Area(f) =>
    attr("area", show(f))

  Bb(Rect(a, b, c, d)) =>
    attr("bb", `${show(a)},${show(b)},${show(c)},${show(d)}`)

  Bgcolor(c) =>
    attr("bgcolor", c)

  Class(c) =>
    attr("class", c)

  ClusterAttr(yes) =>
    attr("cluster", show(yes))

  Color(s) =>
    attr("color", s)

  Colorscheme(s) =>
    attr("colorscheme", s)

  Fillcolor(s) =>
    attr("fillcolor", s)

  Fontcolor(s) =>
    attr("fontcolor", s)

  Fontname(s) =>
    attr("fontname", s)

  Fontsize(f) =>
    attr("fontsize", show(f))

  Gradientangle(i) =>
    attr("gradientangle", show(i))

  Href(s) =>
    attr("href", s)

  Id(s) =>
    attr("id", s)

  K(f) =>
    attr("K", show(f))

  Label(s) =>
    attr("label", s)

  Labeljust(j) =>
    attr("labeljust", justificationToString(j))

  Labelloc(s) =>
    attr("labelloc", s)

  Layer(s) =>
    attr("layer", s)

  Lheight(f) =>
    attr("lheight", show(f))

  Lp(s) =>
    attr("lp", s)

  Lwidth(f) =>
    attr("lwidth", show(f))

  Margin(f) =>
    attr("margin", show(f))

  Nojustify(x) =>
    attr("nojustify", show(x))

  Pencolor(c) =>
    attr("pencolor", c)

  Penwidth(f) =>
    attr("penwidth", show(f))

  Peripheries(i) =>
    attr("peripheries", show(i))

  Sortv(i) =>
    attr("sortv", show(i))

  Style(s) =>
    attr("style", styleLiteralToString(s))

  Target(s) =>
    attr("target", s)

  Tooltip(s) =>
    attr("tooltip", s)

  URL(s) =>
    attr("url", s)

  UnmatchedClusterAttribute =>
    `/* unmatched cluster attribute */`
}

renderAttributes :: List ClusterAttribute -> String
export renderAttributes = pipe(
  map(attributeToString),
  join("\n"),
)
