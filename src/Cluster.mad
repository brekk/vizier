import type { Edge } from "@/Edge"
import type { Rect } from "@/Geometry"
import type { Node } from "@/Node"

import List from "List"
import { join } from "String"

import { literalAttr } from "@/Dot"
import { renderEdges } from "@/Edge"
import { Rect } from "@/Geometry"
import { renderNodes } from "@/Node"
import { indent } from "@/String"



alias Color = String

export type ClusterStyle = ClusterStyleFilled | ClusterStyleStriped | ClusterStyleRounded

export styleLiteralToString = where {
  ClusterStyleFilled =>
    "filled"

  ClusterStyleStriped =>
    "striped"

  ClusterStyleRounded =>
    "rounded"
}

export stringToStyleLiteral = (x) => x == "filled"
  ? ClusterStyleFilled
  : x == "striped" ? ClusterStyleStriped : ClusterStyleRounded

export alias Layer = String
export type Justification = JustificationLeft | JustificationCenter | JustificationRight

export justificationToString = where {
  JustificationLeft =>
    "left"

  JustificationCenter =>
    "center"

  JustificationRight =>
    "right"
}

export type ClusterAttribute
  = Area(Float)
  | Bb(Rect)
  | Bgcolor(Color)
  | Class(String)
  | ClusterAttr(Boolean)
  | Color(Color)
  | Colorscheme(String)
  | Fillcolor(Color)
  | Fontcolor(Color)
  | Fontname(String)
  | Fontsize(Float)
  | Gradientangle(Integer)
  | Href(String)
  | Id(String)
  | K(Float)
  | Label(String)
  | Labeljust(Justification)
  | Labelloc(String)
  | Layer(String)
  | Lheight(Float)
  | Lp(String)
  | Lwidth(Float)
  | Margin(Float)
  | Nojustify(Boolean)
  | Pencolor(Color)
  | Penwidth(Float)
  | Peripheries(Integer)
  | Sortv(Integer)
  | Style(ClusterStyle)
  | Target(String)
  | Tooltip(String)
  | URL(String)
  | UnmatchedClusterAttribute

export type Cluster = Cluster(String, List ClusterAttribute, List Node, List Edge)

toString :: Integer -> Cluster -> String
export toString = (i, c) => where(c) {
  Cluster(label, attrs, nodes, edges) =>
    `
  subgraph cluster_${show(i)} {
    label = "${label}";
    ${renderAttributes(attrs)}
    ${renderNodes(nodes)}
    ${renderEdges(edges)}
  }`
}

renderClusters :: List Cluster -> String
export renderClusters = pipe(
  List.mapWithIndex((x, i) => toString(i, x)),
  join("\n"),
)

attributeToString :: ClusterAttribute -> String
export attributeToString = where {
  Area(f) =>
    literalAttr("area", show(f))

  Bb(Rect(a, b, c, d)) =>
    literalAttr("bb", `${show(a)},${show(b)},${show(c)},${show(d)}`)

  Bgcolor(c) =>
    literalAttr("bgcolor", c)

  Class(c) =>
    literalAttr("class", show(c))

  ClusterAttr(yes) =>
    literalAttr("cluster", show(yes))

  Color(s) =>
    literalAttr("color", s)

  Colorscheme(s) =>
    literalAttr("colorscheme", s)

  Fillcolor(s) =>
    literalAttr("fillcolor", s)

  Fontcolor(s) =>
    literalAttr("fontcolor", s)

  Fontname(s) =>
    literalAttr("fontname", s)

  Fontsize(f) =>
    literalAttr("fontsize", show(f))

  Gradientangle(i) =>
    literalAttr("gradientangle", show(i))

  Href(s) =>
    literalAttr("href", s)

  Id(s) =>
    literalAttr("id", s)

  K(f) =>
    literalAttr("K", show(f))

  Label(s) =>
    literalAttr("label", s)

  Labeljust(j) =>
    literalAttr("labeljust", justificationToString(j))

  Labelloc(s) =>
    literalAttr("labelloc", s)

  Layer(s) =>
    literalAttr("layer", s)

  Lheight(f) =>
    literalAttr("lheight", show(f))

  Lp(s) =>
    literalAttr("lp", s)

  Lwidth(f) =>
    literalAttr("lwidth", show(f))

  Margin(f) =>
    literalAttr("margin", show(f))

  Nojustify(x) =>
    literalAttr("nojustify", show(x))

  Pencolor(c) =>
    literalAttr("pencolor", c)

  Penwidth(f) =>
    literalAttr("penwidth", show(f))

  Peripheries(i) =>
    literalAttr("peripheries", show(i))

  Sortv(i) =>
    literalAttr("sortv", show(i))

  Style(s) =>
    literalAttr("style", styleLiteralToString(s))

  Target(s) =>
    literalAttr("target", s)

  Tooltip(s) =>
    literalAttr("tooltip", s)

  URL(s) =>
    literalAttr("url", s)

  UnmatchedClusterAttribute =>
    `/* unmatched cluster attribute */`
}

renderAttributes :: List ClusterAttribute -> String
export renderAttributes = pipe(
  map(attributeToString),
  join("\n"),
)
